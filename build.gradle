plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'kr.entree.spigradle' version '1.2.4'
    id 'com.diffplug.gradle.spotless' version '3.29.0'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

description 'Simple plugin to let your players make item frames invisible.'
group 'dev.bloodstone'
version '0.0.4'

def mcVersion = '1.15.2-R0.1-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/public/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
    maven { url "https://repo.codemc.org/repository/maven-public" } //bstats
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation group: 'co.aikar', name: 'acf-paper', version: '0.5.0-SNAPSHOT'
    implementation group: 'org.spigotmc', name: 'spigot-api', version: mcVersion
    implementation group: 'dev.bloodstone', name: 'mcutils', version: '0.0.2-SNAPSHOT'
    implementation group: 'ch.jalu', name: 'configme', version: '1.1.0'
    implementation group: 'org.bstats', name: 'bstats-bukkit', version: '1.7'
}

shadowJar {
    String base = "${project.property('group')}.${project.property('name').toString().toLowerCase()}.lib"
    relocate 'co.aikar.commands', "${base}.commands"
    relocate 'co.aikar.locales', "${base}.locales"
    relocate 'dev.bloodstone.mcutils', "${base}.mcutils"
    relocate 'ch.jalu.configme', "${base}.configme"
    relocate 'org.bstats', "${base}.bstats"
}

build {
    dependsOn(shadowJar)
}


spigot {
    authors = ['Prof_Bloodstone']
    apiVersion = '1.15'
}

spotless {
    format 'misc', {
        target '**/*.gradle', '**/*.md'
        trimTrailingWhitespace()
        indentWithSpaces(4)
    }
    kotlin {
        ktlint('0.36.0')
        licenseHeader '/* Licensed under MIT */'
    }
}

compileJava {
    options.encoding = 'UTF-8' // Encode your source code with UTF-8
    options.compilerArgs += ["-parameters"]
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        javaParameters = true
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
